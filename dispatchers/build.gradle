import com.chrynan.dispatchers.buildSrc.LibraryConstants

apply plugin: "com.android.library"
apply plugin: "kotlin-multiplatform"
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    jvm()
    js(BOTH) {
        browser()
        nodejs()
    }
    ios()
    android {
        publishLibraryVariants("release")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"
            }
        }
    }
}

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        minSdkVersion LibraryConstants.Android.minSdkVersion
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://repo.repsy.io/mvn/chrynan/public"

                credentials {
                    username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                    password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId LibraryConstants.group
                artifactId "dispatchers-android"
                version LibraryConstants.versionName
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
